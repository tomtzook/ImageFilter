plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
}

sourceCompatibility = JavaVersion.VERSION_11

group 'ImageFilter'
version '1.0'

repositories {
    mavenCentral()

    maven {
        url('https://frcmaven.wpi.edu/artifactory/release')
    }
}

configurations {
    nativeZip
    implementation.extendsFrom nativeZip
}

dependencies {
    implementation 'com.github.tomtzook:castle:1.0.0'

    implementation group: "edu.wpi.first.thirdparty.frc${YEAR}.opencv", name: 'opencv-java', version: "$OPENCV_VERSION"
    nativeZip group: "edu.wpi.first.thirdparty.frc${YEAR}.opencv", name: 'opencv-jni', version: "$OPENCV_VERSION", classifier: "$REMOTE_ARCH", transitive: false
}

javafx {
    version = '11'
    modules = [ 'javafx.controls' ]
}

application {
    mainClassName = 'imgfilter.Main'
    applicationName = 'imagefilter'
}

task copyNatives(type: Copy) {
    from {
        zipTree(configurations.nativeZip.singleFile).matching {
            include '**/*.so'
            include '**/*.dll'
        }.files
    }
    into project.file("natives")
}

project.processResources.dependsOn copyNatives
